apply plugin: 'com.android.application'
// This does not break the build when Android Studio is missing the JRebel for Android plugin.
apply plugin: 'com.zeroturnaround.jrebel.android'
apply plugin: "maven-publish"

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

//Those are repositories we are looking into when building
//the project
repositories {
    mavenCentral()
    maven {
        //this is a public path into my maven repository
        url "https://dl.dropboxusercontent.com/u/48976508/maven-repo"
    }
}

//here we specify source sets for maven publication
sourceSets {
    //we call this source set a publication , and including only the source
    //folder that we want to ship into jar
    publication {
        java.srcDirs = ['src/main/publish']
    }
}

//this is the task that defines jar that we will publish
task publishJar(type: Jar) {
    from sourceSets.publication.allJava
}

publishing {
    //all publications defined here
    publications {

        //TODO : Run "gradle publish" to upload the Jar to maven repo
        //we call this publication "gameLogicPublication" , and saying that it is a maven publication
        gameLogicPublication(MavenPublication) {
            groupId 'com.yan.durak'
            artifactId 'gamelogic-lib'
            version '1.1-SNAPSHOT'

            //here we define the jar that will be published
            artifact(publishJar) {
                classifier null
            }
        }
    }
    repositories {
        //maven repository is just a local path to the dropbox folder
        //this is where published jar will go
        maven {
            url "file:///E:/Dropbox/public/maven-repo"
//            url "file:///C:/Users/yan.braslavsky/Dropbox/public/maven-repo"
        }
    }
}



android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "com.yan.durak"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "prototype-1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    //we can specify multiple source sets
    sourceSets {
        //in order to compile android project
        //we need to specify a dependency to the source folder that used
        //to generate a jar
        main {
            java.srcDirs = ['src/main/publish','src/main/java']
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        device {
            //Device specific
        }
        emulator {
            //Emulator specific
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:+'
    compile 'com.yan.glengine:engine-lib:1.0-SNAPSHOT@aar'
    compile 'com.koushikdutta.async:androidasync:2.+'
}